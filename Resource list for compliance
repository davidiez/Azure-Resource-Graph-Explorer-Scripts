// Resource list for compliance
resourcecontainers
| where type =~ "microsoft.resources/subscriptions"
| project subscriptionId, subscriptionName = name, owner = tostring(tags['owner'])
| join kind=inner (
    resources
    | project subscriptionId, resourceGroup, type, name, location, tags, id
    | extend tagCount = coalesce(array_length(bag_keys(tags)), 0)
    | extend hasTags = tagCount > 0
    | extend cost_center = tostring(tags['cost_center'])
) on subscriptionId
| project subscriptionName, subscriptionId, owner, name, resourceGroup, type, id, location, tags, tagCount, hasTags, cost_center
